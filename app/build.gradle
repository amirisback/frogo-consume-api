plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {

    compileSdk projectCompileSdk

    defaultConfig {
        applicationId projectApplicationId
        minSdk projectMinSdk
        targetSdk projectTargetSdk
        versionCode projectVersionCode
        versionName projectVersionName

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        // Declaration apps name debug mode
        def debugAttribute = " Development"
        def nameAppDebug = nameApp + debugAttribute

        // Naming APK // AAB
        setProperty("archivesBaseName", "${nameAPK}(${versionName})#${gitCurrentBranch()}")

        resConfigs "en", "id"

        // Inject app name for debug
        resValue "string", "app_name", nameAppDebug

        // Declaration admob id for debug
        def debugAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
        def debugAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
        def debugAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"
        def debugAdmobInterstitialVideo = "ca-app-pub-3940256099942544/8691691433"
        def debugAdmobRewarded = "ca-app-pub-3940256099942544/5224354917"
        def debugAdmobRewardedInterstitial = "ca-app-pub-3940256099942544/5354046379"
        def debugAdmobNativeAdvanced = "ca-app-pub-3940256099942544/2247696110"
        def debugAdmobNativeAdvancedVideo = "ca-app-pub-3940256099942544/1044960115"

        // Inject admob id for debug
        resValue "string", "admob_publisher_id", debugAdmobPublisherId
        resValue "string", "admob_banner", debugAdmobBanner
        resValue "string", "admob_interstitial", debugAdmobInterstitial
        resValue "string", "admob_interstitial_video", debugAdmobInterstitialVideo
        resValue "string", "admob_rewarded", debugAdmobRewarded
        resValue "string", "admob_rewarded_interstitial", debugAdmobRewardedInterstitial
        resValue "string", "admob_native_advanced", debugAdmobNativeAdvanced
        resValue "string", "admob_native_advanced_video", debugAdmobNativeAdvancedVideo
    }

    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            // [PROJECT FOLDER NAME/app/[COPY YOUT KEY STORE] .jks in here
            storeFile file("frogoboxdev.jks")
            storePassword "cronoclez"
            keyAlias "frogobox"
            keyPassword "xeonranger"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Generated Signed APK / AAB
            signingConfig signingConfigs.release

            // Inject app name for release
            resValue "string", "app_name", nameApp

            // Declaration admob id for release
            def releaseAdmobPublisherId = ""
            def releaseAdmobBanner = ""
            def releaseAdmobInterstitial = ""
            def releaseAdmobInterstitialVideo = ""
            def releaseAdmobRewarded = ""
            def releaseAdmobRewardedInterstitial = ""
            def releaseAdmobNativeAdvanced = ""
            def releaseAdmobNativeAdvancedVideo = ""

            // Inject admob id for release
            resValue "string", "admob_publisher_id", releaseAdmobPublisherId
            resValue "string", "admob_banner", releaseAdmobBanner
            resValue "string", "admob_interstitial", releaseAdmobInterstitial
            resValue "string", "admob_interstitial_video", releaseAdmobInterstitialVideo
            resValue "string", "admob_rewarded", releaseAdmobRewarded
            resValue "string", "admob_rewarded_interstitial", releaseAdmobRewardedInterstitial
            resValue "string", "admob_native_advanced", releaseAdmobNativeAdvanced
            resValue "string", "admob_native_advanced_video", releaseAdmobNativeAdvancedVideo
        }
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        useIR = true
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

static def gitCurrentBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation project(':frogoconsumeapi')

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.material:material:1.4.0'

    implementation 'de.hdodenhof:circleimageview:3.1.0'

    implementation 'com.github.bumptech.glide:glide:4.12.0'

    implementation 'com.github.amirisback:frogo-recycler-view:3.8.5'
    implementation 'com.github.amirisback:frogo-log:2.0.4'

    implementation 'com.github.frogobox:frogo-android-ui-kit:1.0.4'
    implementation 'com.github.frogobox:frogo-android-sdk:2.0.0'

    implementation "io.insert-koin:koin-core:$koin_version" // Koin core features
    implementation "io.insert-koin:koin-android:$koin_version" // Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android-compat:$koin_version" // Koin Java Compatibility
    implementation "io.insert-koin:koin-androidx-workmanager:$koin_version" // Koin for Jetpack WorkManager
    implementation "io.insert-koin:koin-androidx-compose:$koin_version" // Koin for Jetpack Compose

    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}